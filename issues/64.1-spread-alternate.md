Alternate suggestion for spread syntax: `.*`.

# Discussion
A new suggestion for the **spread** symbol `.*` has the familiarity of the bash-like glob selector. `x.*` has the same precedence as the dot-accessor (#25), but it’s not technically an accessor; it stands for “select all the elements in `x`”. For example, the entries of `[!x.*, y ^ z.*, x.*.0, z.* ^ y]` are all ill-formed. The `.*` syntax *must only* be used on compound expressions (awaits, property accesses, and function calls), and it must be the last “operation” in a collection’s entry. The entries of `[(!x).*, (y ^ z).*, x.0.*, 42 . *]` are all well-formed (even if type-invalid).

This proposal suggests using the same symbol `.*` for all 3 kinds of spreads: tuples, records, and mappings. With the current grammar, this would make an expression such as `[x.*, y.*, z.*]` syntactically ambiguous — Is it a tuple literal, record literal, or mapping literal? Therefore we need a new syntax production for a literal containing only spread items (whose compile-time type could not be determined by syntax), and then modify the tuple, record, and mapping literal productions such that they must, respectively, contain at least 1 expression, property, or case.

## Tuple Spread
```
let cde: [str, str, str] = [c, d, e]; % assume these variables are strings
let abcdefg = [str, str, str, str, str, str, str] = [a, b, cde.*, f, g];
abcdefg == [a, b, c, d, e, f, g];
```

## Record Spread
```
type Car = [
	make:  str,
	model: str,
	color: str,
	year:  int,
];
let my_car: Car = [
	make=  'Mazda',
	model= 'Protegé',
	color= 'black',
	year=  2003,
];
let new_car: Car = [
	color= 'red',
	my_car.*,
	year= 2010,
];
new_car == [
	make=  'Mazda',
	model= 'Protegé',
	color= 'black',
	year=  2010,
];
```

## Mapping Spread
```
let office: obj = [
	jim    |-> pam,
	dwight |-> angela,
	andy   |-> erin,
];
let parks: obj = [
	leslie |-> ben,
	ann    |-> chris,
	april  |-> andy,
];
[office.*, parks.*] == [
	jim    |-> pam,
	dwight |-> angela,
	andy   |-> erin,
	leslie |-> ben,
	ann    |-> chris,
	april  |-> andy,
];
```


## Errors
```
let failure: [str, str, str, str, str] = [a, [c, d, e].*]; %> TypeError (count 4 not assignable to count 5)
let failure: [str, str, str]           = [a, [c, d, e].*]; %> TypeError (count 4 not assignable to count 3)
```
```
let failure: [a: int, b: bool, c: str] = [a= 42, [b= 'bool', c= true].*];   %> TypeError: `true` not assignable to `str`
let failure: [a: int, b: bool, c: str] = [a= 42, [b= true, d= 'string'].*]; %> TypeError: missing property `c`
```
```
[1, [b= 2, c= 3].*];           %> TypeError: record  spread into tuple
[[b= 2, c= 3].*, 1];           %> TypeError: record  spread into tuple
[1, ['b' |-> 2, 'c' |-> 3].*]; %> TypeError: mapping spread into tuple
[['b' |-> 2, 'c' |-> 3].*, 1]; %> TypeError: mapping spread into tuple
[a= 1, [2, 3].*];                 %> TypeError: tuple   spread into record
[[2, 3].*, a= 1];                 %> TypeError: tuple   spread into record
[a= 1, ['b' |-> 2, 'c' |-> 3].*]; %> TypeError: mapping spread into record
[['b' |-> 2, 'c' |-> 3].*, a= 1]; %> TypeError: mapping spread into record
['a' |-> 1, [2, 3].*];       %> TypeError: tuple  spread into mapping
[[2, 3].*, 'a' |-> 1];       %> TypeError: tuple  spread into mapping
['a' |-> 1, [b= 2, c= 3].*]; %> TypeError: record spread into mapping
[[b= 2, c= 3].*, 'a' |-> 1]; %> TypeError: record spread into mapping

[[b= 2, c= 3].*, [1].*];           %> TypeError: tuple spread into record
[['b' |-> 2, 'c' |-> 3].*, [1].*]; %> TypeError: tuple spread into mapping
[[2, 3].*, [a= 1].*];                 %> TypeError: record spread into tuple
[['b' |-> 2, 'c' |-> 3].*, [a= 1].*]; %> TypeError: record spread into mapping
[[2, 3].*, ['a' |-> 1].*];       %> TypeError: mapping spread spread into tuple
[[b= 2, c= 3].*, ['a' |-> 1].*]; %> TypeError: mapping spread spread into record
```

# Specification

## Syntax
```diff
+Spread
+	::= ExpressionCompound "." "*";

-ListLiteral      ::= "[" ","?                Expression#                              ","? "]";
-RecordLiteral    ::= "[" ","?                Property#                                ","? "]";
-MappingLiteral   ::= "[" ","?                Case#                                    ","? "]";
+CollectionSpread ::= "[" ","?                Spread#                                  ","? "]";
+ListLiteral      ::= "[" ","? (Spread# ",")? Expression ("," (Spread | Expression)#)? ","? "]";
+RecordLiteral    ::= "[" ","? (Spread# ",")? Property   ("," (Spread | Property)#)?   ","? "]";
+MappingLiteral   ::= "[" ","? (Spread# ",")? Case       ("," (Spread | Case)#)?       ","? "]";

ExpressionUnit ::=
	| "[" "]"
	| IDENTIFIER
	| PrimitiveLiteral
	| StringTemplate
+	| CollectionSpread
	| ListLiteral
	| RecordLiteral
	| MappingLiteral
	| "(" Expression ")"
;

ExpressionCompound ::=
	| ExpressionUnit
	| ExpressionCompound PropertyAccess
;
```

## Semantics
```diff
+SemanticSpread
+	::= Expression;

SemanticEmptyCollection   ::= ();
-SemanticList             ::= SemanticExpression+;
-SemanticRecord           ::= SemanticProperty+;
-SemanticMapping          ::= SemanticCase+;
+SemanticSpreadCollection ::= SemanticSpread+;
+SemanticList             ::= SemanticSpread* SemanticExpression (SemanticSpread | SemanticExpression)*;
+SemanticRecord           ::= SemanticSpread* SemanticProperty   (SemanticSpread | SemanticProperty)*;
+SemanticMapping          ::= SemanticSpread* SemanticCase       (SemanticSpread | SemanticCase)*;
```

## Decorate
```diff
+Decorate(Spread ::= ExpressionCompound "." "*") -> SemanticSpread
+	:= (SemanticSpread Decorate(ExpressionCompound));

+Decorate(CollectionSpread ::= "[" ","? CollectionSpread__0__List ","? "]") -> SemanticSpreadCollection
+	:= (SemanticSpreadCollection
+		...Decorate(CollectionSpread__0__List)
+	);

+	Decorate(CollectionSpread__0__List ::= Spread) -> Tuple<SemanticSpread>
+		:= [Decorate(Spread)];
+	Decorate(CollectionSpread__0__List ::= CollectionSpread__0__List "," Spread) -> Sequence<SemanticSpread>
+		:= [
+			...Decorate(CollectionSpread__0__List),
+			Decorate(Spread),
+		];

-Decorate(ListLiteral ::= "[" ","? ListLiteral__0__List ","? "]") -> SemanticList
-	:= (SemanticList
-		...Decorate(ListLiteral__0__List)
-	);
+Decorate(ListLiteral ::= "[" ","? Expression ","? "]") -> SemanticList
+	:= (SemanticList Decorate(Expression));
+Decorate(ListLiteral ::= "[" ","? Expression "," ListLiteral__0__List ","? "]") -> SemanticList
+	:= (SemanticList
+		Decorate(Expression)
+		...Decorate(ListLiteral__0__List)
+	);
+Decorate(ListLiteral ::= "[" ","? CollectionSpread__0__List "," Expression ","? "]") -> SemanticList
+	:= (SemanticList
+		...Decorate(CollectionSpread__0__List)
+		Decorate(Expression)
+	);
+Decorate(ListLiteral ::= "[" ","? CollectionSpread__0__List "," Expression "," ListLiteral__0__List ","? "]") -> SemanticList
+	:= (SemanticList
+		...Decorate(CollectionSpread__0__List)
+		Decorate(Expression)
+		...Decorate(ListLiteral__0__List)
+	);

	Decorate(ListLiteral__0__List ::= Expression) -> Tuple<SemanticExpression>
		:= [Decorate(Expression)];
+	Decorate(ListLiteral__0__List ::= Spread) -> Tuple<SemanticSpread>
+		:= [Decorate(Spread)];
-	Decorate(ListLiteral__0__List ::= ListLiteral__0__List "," Expression) -> Sequence<SemanticExpression>
+	Decorate(ListLiteral__0__List ::= ListLiteral__0__List "," Expression) -> Sequence<SemanticExpression | SemanticSpread>
		:= [
			...Decorate(ListLiteral__0__List),
			Decorate(Expression),
		];
+	Decorate(ListLiteral__0__List ::= ListLiteral__0__List "," Spread) -> Sequence<SemanticExpression | SemanticSpread>
+		:= [
+			...Decorate(ListLiteral__0__List),
+			Decorate(Spread),
+		];

-Decorate(RecordLiteral ::= "[" ","? RecordLiteral__0__List ","? "]") -> SemanticRecord
-	:= (SemanticRecord
-		...Decorate(RecordLiteral__0__List)
-	);
+Decorate(RecordLiteral ::= "[" ","? Property ","? "]") -> SemanticRecord
+	:= (SemanticRecord Decorate(Property));
+Decorate(RecordLiteral ::= "[" ","? Property "," RecordLiteral__0__List ","? "]") -> SemanticRecord
+	:= (SemanticRecord
+		Decorate(Property)
+		...Decorate(RecordLiteral__0__List)
+	);
+Decorate(RecordLiteral ::= "[" ","? CollectionSpread__0__List "," Property ","? "]") -> SemanticRecord
+	:= (SemanticRecord
+		...Decorate(CollectionSpread__0__List)
+		Decorate(Property)
+	);
+Decorate(RecordLiteral ::= "[" ","? CollectionSpread__0__List "," Property "," RecordLiteral__0__List ","? "]") -> SemanticRecord
+	:= (SemanticRecord
+		...Decorate(CollectionSpread__0__List)
+		Decorate(Property)
+		...Decorate(RecordLiteral__0__List)
+	);

	Decorate(RecordLiteral__0__List ::= Property) -> Tuple<SemanticProperty>
		:= [Decorate(Property)];
+	Decorate(RecordLiteral__0__List ::= Spread) -> Tuple<SemanticSpread>
+		:= [Decorate(Spread)];
-	Decorate(RecordLiteral__0__List ::= RecordLiteral__0__List "," Property) -> Sequence<SemanticProperty>
+	Decorate(RecordLiteral__0__List ::= RecordLiteral__0__List "," Property) -> Sequence<SemanticProperty | SemanticSpread>
		:= [
			...Decorate(RecordLiteral__0__List),
			Decorate(Property),
		];
+	Decorate(RecordLiteral__0__List ::= RecordLiteral__0__List "," Spread) -> Sequence<SemanticProperty | SemanticSpread>
+		:= [
+			...Decorate(RecordLiteral__0__List),
+			Decorate(Spread),
+		];

-Decorate(MappingLiteral ::= "[" ","? MappingLiteral__0__List ","? "]") -> SemanticMapping
-	:=(SemanticMapping
-		...Decorate(MappingLiteral__0__List)
-	);
+Decorate(MappingLiteral ::= "[" ","? Case ","? "]") -> SemanticMapping
+	:= (SemanticMapping Decorate(Case));
+Decorate(MappingLiteral ::= "[" ","? Case "," MappingLiteral__0__List ","? "]") -> SemanticMapping
+	:= (SemanticMapping
+		Decorate(Case)
+		...Decorate(MappingLiteral__0__List)
+	);
+Decorate(MappingLiteral ::= "[" ","? CollectionSpread__0__List "," Case ","? "]") -> SemanticMapping
+	:= (SemanticMapping
+		...Decorate(CollectionSpread__0__List)
+		Decorate(Case)
+	);
+Decorate(MappingLiteral ::= "[" ","? CollectionSpread__0__List "," Case "," MappingLiteral__0__List ","? "]") -> SemanticMapping
+	:= (SemanticMapping
+		...Decorate(CollectionSpread__0__List)
+		Decorate(Case)
+		...Decorate(MappingLiteral__0__List)
+	);

	Decorate(MappingLiteral__0__List ::= Case) -> Tuple<SemanticCase>
		:= [Decorate(Case)];
+	Decorate(MappingLiteral__0__List ::= Spread) -> Tuple<SemanticSpread>
+		:= [Decorate(Spread)];
-	Decorate(MappingLiteral__0__List ::= MappingLiteral__0__List "," Case) -> Sequence<SemanticCase>
+	Decorate(MappingLiteral__0__List ::= MappingLiteral__0__List "," Case) -> Sequence<SemanticCase | SemanticSpread>
		:= [
			...Decorate(MappingLiteral__0__List),
			Decorate(Case),
		];
+	Decorate(MappingLiteral__0__List ::= MappingLiteral__0__List "," Spread) -> Sequence<SemanticCase | SemanticSpread>
+		:= [
+			...Decorate(MappingLiteral__0__List),
+			Decorate(Spread),
+		];
```

# Workaround
```
let tup: [int, int, int] = [2, 3, 4];
[1, tup.0, tup.1, tup.2, 5]; % [1, tup.*, 5]
```
